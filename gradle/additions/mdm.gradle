import java.lang.management.ManagementFactory
import java.lang.management.RuntimeMXBean
import java.nio.file.Files

task('sysProps') {
    group 'help'
    doLast {
        logger.quiet('{}', System.properties.collect {"${it.key}:${it.value}"}.sort().join('\n'))
    }
}

task('jvmArgs') {
    group 'help'
    doLast {
        RuntimeMXBean runtimeMxBean2 = ManagementFactory.getRuntimeMXBean()
        logger.quiet('{}', runtimeMxBean2.getInputArguments().join('\n'))
    }
}

// use mdm-core to supply the notice template
java.nio.file.Path noticePath = Files.createTempFile('notice', '.tmpl')
Files.write(noticePath,
            'https://raw.githubusercontent.com/MauroDataMapper/mdm-core/develop/gradle/NOTICE.tmpl'.toURL().readLines())

license {
    header = file(noticePath)
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    excludes([
        '**/*.gson',
        '**/*.yml',
        '**/META-INF/**',
        '**/*.txt',
        '**/*.json',
        '**/*.gml',
        '**/*.jpeg',
        '**/*.png',
        '**/*.xml',
        '**/*.sql',
        '**/*.csv',
        '**/*.xsd'
    ])
}

afterEvaluate {
    tasks.withType(Test) {testTask ->
        ignoreFailures = System.getenv().containsKey('JENKINS')
    }
}